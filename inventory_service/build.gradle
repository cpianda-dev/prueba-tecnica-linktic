plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.linktic'
version = '1.0.0'
description = 'first technical test microservice: inventory-service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
}

jacoco {
    toolVersion = "0.8.12"
}

def jacocoExcludes = [
        '**/InventoryApplication.*',
        '**/config/OpenApiConfig.*',
        '**/infrastructure/persistence/entity/**',
        '**/*MapperImpl.*',
        '**/infrastructure/mapper/**',
]

def mainOutputDirs = [
        "$buildDir/classes/java/main",
        "$buildDir/generated/sources/annotationProcessor/java/main"
]

tasks.named('jacocoTestReport') {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    classDirectories.setFrom(files(
            mainOutputDirs.collect { dir -> fileTree(dir: dir, exclude: jacocoExcludes) }
    ))
}

tasks.named('jacocoTestCoverageVerification') {
    classDirectories.setFrom(files(
            mainOutputDirs.collect { dir -> fileTree(dir: dir, exclude: jacocoExcludes) }
    ))
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value   = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestCoverageVerification
